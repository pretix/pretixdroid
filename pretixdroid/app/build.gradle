apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "eu.pretix.pretixdroid"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 4
        versionName "1.3"
        vectorDrawables.useSupportLibrary = true
    }

    lintOptions {
        disable 'InvalidPackage' // problem with jetty and bouncycastle
    }

    signingConfigs {
        release {
            storeFile file("../../release.keystore")
            storePassword System.getenv("KSTOREPWD")
            keyAlias "pretixdroid"
            keyPassword System.getenv("KSTOREPWD")
        }
    }

    buildTypes {
		def sentry_dsn = "";
		try {
			Properties properties = new Properties()
			properties.load(project.rootProject.file('local.properties').newDataInputStream())
			sentry_dsn = properties.getProperty('sentry.dsn')
			if (sentry_dsn == null) {
				sentry_dsn = "null"
			} else {
				sentry_dsn = "\"" + sentry_dsn + "\""
			}
		} catch (all) {
			sentry_dsn = "null"
		}

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            buildConfigField "String", "SENTRY_DSN", sentry_dsn
        }
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            buildConfigField "String", "SENTRY_DSN", "null"
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:25.2.0'
    compile 'com.android.support:design:25.2.0'
    compile 'me.dm7.barcodescanner:zxing:1.9'
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'com.android.support:support-v4:25.2.0'
    compile 'com.android.support:support-vector-drawable:25.2.0'
    compile 'com.joshdholtz.sentry:sentry-android:1.6.0'
}
